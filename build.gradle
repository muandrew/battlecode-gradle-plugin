// Handle uploading to the battlecode maven repo with a credentials file
buildscript {
    repositories { jcenter() }
    dependencies { classpath 'com.amazonaws:aws-java-sdk-core:1.11.5' }
}

// amu
plugins {
    id 'com.jfrog.bintray' version '1.7.3'
}

// Apply the groovy plugin to add support for Groovy
apply plugin: 'groovy'
apply plugin: 'maven-publish'

group = 'org.battlecode'
version = '2017.0.0.7'

// amu
group = 'com.muandrew.battlecode'
def siteUrl = 'https://github.com/muandrew/battlecode-gradle-plugin'
def gitUrl = 'https://github.com/muandrew/battlecode-gradle-plugin.git'
def publishVersion = version

sourceCompatibility = 1.8
targetCompatibility = 1.8

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // Use the currently executing groovy
    compile localGroovy()

    // we need the gradle API
    compile gradleApi()

    testCompile 'junit:junit:4.12'

    testCompile 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.6'

}

import com.amazonaws.auth.*
import com.amazonaws.auth.profile.*


def fetchAwsCredentials = {
    try {
        return new ProfileCredentialsProvider().credentials
    } catch (Exception exception) {
        logger.debug('Unable to retrieve AWS credentials from profile, publishing to S3 will not be available.')
        return null
    }
}

// amu
task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java

            // amu
            artifact sourcesJar
            artifact javadocJar
            artifactId 'battlecode-gradle-plugin'
        }
    }
}

//amu
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['plugin']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'battlecode-gradle-plugin'
        desc = 'Useful battlecode tasks! (In theory.)'
        websiteUrl = siteUrl
        issueTrackerUrl = siteUrl + '/issues'
        vcsUrl = gitUrl
        licenses = ['AGPL-V3']
        labels = ['battlecode']
        publicDownloadNumbers = true
        version {
            name = publishVersion
            released = new java.text.SimpleDateFormat('yyyy-MM-dd\'T\'HH:mm:ss.SSSZZ').format(new Date())
            vcsTag = 'v' + publishVersion
        }
    }
}

AWSCredentials awsCredentials = fetchAwsCredentials()
if (awsCredentials != null) {
    publishing {
        repositories {
            maven {
                name 's3'
                url 's3://battlecode-maven'
                credentials(AwsCredentials) {
                    accessKey awsCredentials.AWSAccessKeyId
                    secretKey awsCredentials.AWSSecretKey
                }
            }
            maven {
                name 's3-backcompat'
                url 's3://battlecode-maven-test-aaarblgarbl'
                credentials(AwsCredentials) {
                    accessKey awsCredentials.AWSAccessKeyId
                    secretKey awsCredentials.AWSSecretKey
                }
            }
        }
    }
}
